#packopt name "qwerty3x"
#packopt hide 1
#packopt lang "1041"
#packopt icon "appicon.ico"
#packopt version "infomationversion.txt"
#packopt manifest "DPI.manifest"

#include "user32.as"
#uselib "user32.dll"
#func SetProcessDPIAware "SetProcessDPIAware"
#func global SetProcessDpiAwarenessContext "SetProcessDpiAwarenessContext" int
#include "kernel32.as"
#func global GetNativeSystemInfo "GetNativeSystemInfo" sptr
#include "shell32.as"
#include "gdi32.as"
#include "winmm.as"
#include "hspext.as"
#include "hspinet.as"
#include "obj.as"
#include "hspmath.as"
#define CLSID_ActiveDesktop "{75048700-EF1F-11D0-9888-006097DEACF9}"
#define IID_IActiveDesktop "{F490EB00-1240-11D1-9888-006097DEACF9}"
#include "advapi32.as"
#include "mod_fontdlg.as"

*systemstartup;qwerty3xを起動する

#ifdef _debug
	hspdebugmode=1
#else
	hspdebugmode=0
#endif

#include "module/include code_1_module.hsp";281
#include "module/include code_2_default.hsp";11
;#include "module/include code_3_buffer.hsp";320
;#include "module/include code_4_controlmakescreen.hsp";37
;281+11+320+37=649 649+1022=1671

	qwertyver="developing_alpha_1671"
	qwertybuild="1671"
	qwertydate="2025/06/22"
	qwertycur=dir_cur
	qwertypath=""+dir_cur+"\\qwerty3x.exe"
	passwordkey=96
	randomize

	gosub *loadingsettingfile

	screen 0000,ginfo(20),ginfo(21),0
	hclient=hwnd
	getwindowlong hwnd,-16
	setwindowlong hwnd,-16,stat-$80000|$CF0000
	title "Starting... | qwerty3x ver."+qwertyver
	width 800,500,10,10

	gsel 0000,int(loadset("ALWAYSFRONT",settingdata))+1

	gosub *langstrrepupdate

	title ""+langstr(1002)+" | qwerty3x ver."+qwertyver;1002=起動中

	windowsname=""
	if sysinfo(0)="WindowsNT ver6.3"{okfullmode=1:windowsver="NT6.3":windowsname="Windows 8.1 / Server2012 R2"}
	if sysinfo(0)="WindowsNT ver6.2"{okfullmode=1:windowsver="NT6.2":windowsname="Windows 8 / Server2012"}
	if sysinfo(0)="WindowsNT ver6.1"{okfullmode=1:windowsver="NT6.1":windowsname="Windows 7 / Server2008 R2"}
	if sysinfo(0)="WindowsNT ver6.0"{okfullmode=0:windowsver="NT6.0":windowsname="Windows Vista / Server2008"}
	if sysinfo(0)="WindowsNT ver5.2"{okfullmode=0:windowsver="NT5.2":windowsname="Windows Server2003 / XP x64Edition"}
	if sysinfo(0)="WindowsNT ver5.1"{okfullmode=0:windowsver="NT5.1":windowsname="Windows XP"}
	if sysinfo(0)="WindowsNT ver5.0"{okfullmode=-1:windowsver="NT5.0":windowsname="Windows 2000"}
	if sysinfo(0)="WindowsNT ver4.0"{okfullmode=-1:windowsver="NT4.0":windowsname="Windows NT 4.0"}
	if sysinfo(0)="WindowsNT ver3.51"{okfullmode=-1:windowsver="NT3.51":windowsname="Windows NT 3.51"}
	if sysinfo(0)="WindowsNT ver3.5"{okfullmode=-1:windowsver="NT3.5":windowsname="Windows NT 3.5"}
	if sysinfo(0)="WindowsNT ver3.1"{okfullmode=-1:windowsver="NT3.1":windowsname="Windows NT 3.1"}
	if sysinfo(0)="Windows9X ver4.90"{okfullmode=-1:windowsver="4.90":windowsname="Windows Me"}
	if sysinfo(0)="Windows9X ver4.10"{okfullmode=-1:windowsver="4.10":windowsname="Windows 98 / 98 Second Edition"}
	if sysinfo(0)="Windows9X ver4.0"{okfullmode=-1:windowsver="4.0":windowsname="Windows 95"}
	if windowsname="":{
		okfullmode=2
		windowsver="NT10.0"
		#uselib "ntdll.dll"
		#func RtlGetVersion "RtlGetVersion" sptr
		// バージョン取得
		dim OSVERSIONINFO, 37
		OSVERSIONINFO = varsize(OSVERSIONINFO)
		RtlGetVersion varptr(OSVERSIONINFO)
		majorVersion = OSVERSIONINFO(1)
		buildNumber = OSVERSIONINFO(3)
		// 判定
		if (majorVersion >= 10) {
			if (buildNumber >= 22000) {
				windowsname="Windows 11"
			}else{
				if buildNumber=20348:{
					windowsname="Windows Server 2022"
				}else:if buildNumber=17763:{
					windowsname="Windows Server 2019"
				}else:if buildNumber=14393:{
					windowsname="Windows Server 2016"
				}else{
					windowsname="Windows 10"
				}
			}
			;if buildNumber>=18362:{;Windows10 1903以降
			;}else{
			;}
		}
		#define WM_COMMAND $111
	}
	if windowsname="Windows 11":{
		#uselib "dwmapi.dll"
		#func global DwmSetWindowAttribute "DwmSetWindowAttribute" int, int, int, int
		#func global DwmExtendFrameIntoClientArea "DwmExtendFrameIntoClientArea" int,int
		#define DWMWA_USE_HOSTBACKDROPBRUSH 17            
		#define DWMWA_USE_IMMERSIVE_DARK_MODE 20
		#define DWMWA_SYSTEMBACKDROP_TYPE 38
		
		#enum DWMWA_BORDER_COLOR = 34
		#enum DWMWA_CAPTION_COLOR
		#enum DWMWA_TEXT_COLOR
	}

	gosub *langstrrepupdate
	if okfullmode=-1:{
		dialog langstr(1001),0,"qwerty3x ver."+qwertyver
		;1001=サポートしていないWindowsバージョンを検出しました：windowsname
		;起動は続行できますが、想定していない動作が発生する可能性があります。
	}
	if okfullmode=2:{
		;fontautoname="Yu Gothic UI"
		fontautoname="Yu Gothic UI"
	}else:if okfullmode=1:{
		fontautoname="Meiryo UI"
	}else{
		fontautoname="MS UI Gothic"
	}
	if loadset("FONTNAME",settingdata)="AUTO":{
		fontstr=fontautoname
	}else{
		fontstr=loadset("FONTNAME",settingdata)
	}

	if windowsname="Windows 11":{
		if loadset("DISPCOLORMODE",settingdata)=0:{;ダークモード
			opt=rgb(32,32,32)
			dwmsetwindowattribute hwnd,DWMWA_CAPTION_COLOR,varptr(opt),4
			dwmsetwindowattribute hwnd,DWMWA_BORDER_COLOR,varptr(opt),4
			opt=rgb(255,255,255)
			dwmsetwindowattribute hwnd,DWMWA_TEXT_COLOR,varptr(opt),4
		}else:if loadset("DISPCOLORMODE",settingdata)=1:{;ライトモード
			opt=rgb(255,255,255)
			dwmsetwindowattribute hwnd,DWMWA_CAPTION_COLOR,varptr(opt),4
			dwmsetwindowattribute hwnd,DWMWA_BORDER_COLOR,varptr(opt),4
			opt=rgb(0,0,0)
			dwmsetwindowattribute hwnd,DWMWA_TEXT_COLOR,varptr(opt),4
		}
	}

	#define logpixelsx $00000058
	#define logpixelsy $0000005A
	#define ctype xsiz(%1) int(round(double(%1)*xdpi/96))
	#define ctype ysiz(%1) int(round(double(%1)*ydpi/96))
	#define ctype sizx(%1) int(%1*96/xdpi)
	#define ctype sizy(%1) int(%1*96/ydpi)
	if okfullmode=0:{;Windows XP、Windows Vista
		xdpi2=96
		ydpi2=96
		nowxsize="100%(OS側の画面拡大率設定非対応に基づく)"
		nowxsizebuf=100
		xdpi=96
		ydpi=96
	}else{
		setprocessdpiaware
		xdpi2=getdevicecaps(hdc,logpixelsx)
		ydpi2=getdevicecaps(hdc,logpixelsy)
		xdpi=xdpi2
		ydpi=ydpi2
		nowxsize=""+xsiz(100)+"%(システム設定に基づく)"
		nowxsizebuf=xsiz(100)
	}
	dismaxx=ginfo(20)
	dismaxy=ginfo(21)

	screen 0000,ginfo(20),ginfo(21),0
	getwindowlong hwnd,-16
	setwindowlong hwnd,-16,stat|$10000|$40000
	getwindowlong hwnd,-16
	setwindowlong hwnd,-16,stat-$80000
	title ""+langstr(1002)+" | qwerty3x ver."+qwertyver;起動中

	bgscrwindowmax=3
	repeat bgscrwindowmax
		bgscr (cnt+1),ginfo(20),ginfo(21),2
		hchild.cnt=hwnd
		getwindowlong hwnd,-16
		setwindowlong hwnd,-16,stat|$CF0000
		setparent hchild.cnt,hclient
		getwindowlong hwnd,-16
		setwindowlong hwnd,-16,stat-$800000-$40000
	loop;0001 0002 0003

	gsel 0000,int(loadset("ALWAYSFRONT",settingdata))+1
	width int(loadset("WINDOW_SIZX",settingdata)),int(loadset("WINDOW_SIZY",settingdata)),int(loadset("WINDOW_POSX",settingdata)),int(loadset("WINDOW_POSY",settingdata))
	if loadset("ISZOOMED",settingdata)="1":{
		sendmsg hwnd,$112,$F030
	}

	IsZoomed hwnd
	iszoom=stat
	screenname=langstr(3001);ランチャー
	fontsize=ysiz(15)
	conpage=0
	conpage2=0

	if ginfo(12)<=xsiz(500)|ginfo(13)<=ysiz(500):{
		width xsiz(500),ysiz(500)
	}

	gosub *bufferallupdate

	gosub *makescreen

	goto *systemrepeat

*makescreen;画面新規更新
	redraw 0

	getkey ctrl,17
	if ctrl=1:{
		if qwertydebag=0:{
			qwertydebag=1
		}else{
			qwertydebag=0
		}
	}

	if qwertydebag=1:{
		title "<デバッグ>No.1 :フォント名を指定中/Specifying font name | qwerty3x ver."+qwertyver
	}
	if loadset("FONTNAME",settingdata)="AUTO":{
		fontstr=fontautoname
	}else{
		fontstr=loadset("FONTNAME",settingdata)
	}

	if qwertydebag=1:{
		title "<デバッグ>No.2 :スクリーンバッファを作成中/Creating screen buffer | qwerty3x ver."+qwertyver
	}
	gosub *bufferallupdate

	if qwertydebag=1:{
		title "<デバッグ>No.3 :画面クリア中/Clearing screen | qwerty3x ver."+qwertyver
	}	
	repeat bgscrwindowmax
		gsel (cnt+1),-1
	loop
	gsel 0000,int(loadset("ALWAYSFRONT",settingdata))+1
	enablewindow hclient,0
	clrobj
	color 0,0,0
	boxf

	if qwertydebag=1:{
		title "<デバッグ>No.4 :フォントを設定中/Setting font | qwerty3x ver."+qwertyver
	}
	font fontstr,fontsize,0,0
	objmode 2,1

	if qwertydebag=1:{
		title "<デバッグ>No.5 :タイトルバーを描画中/Drawing titlebar | qwerty3x ver."+qwertyver
	}
	pos xsiz(0),ysiz(0)
	boxfofmes int(loadset("DISPCOLORMODE",settingdata)),xsiz(0),ysiz(0),ginfo(12),ysiz(30)
	line ginfo(12),ysiz(30),xsiz(0),ysiz(30)

	pos ginfo(12)-xsiz(30),ysiz(0)
	objsize xsiz(30),ysiz(30)
	objimage 1008,xsiz(90),ysiz(59),xsiz(90),ysiz(59),xsiz(90),ysiz(60)
	button "",*systemshutdown
	endadr=stat
	pos ginfo(12)-xsiz(60),ysiz(0)
	if iszoom=0:{
		objimage 1008,xsiz(30),ysiz(59),xsiz(30),ysiz(59),xsiz(30),ysiz(60)
	}else{
		objimage 1008,xsiz(60),ysiz(59),xsiz(60),ysiz(59),xsiz(60),ysiz(60)
	}
	button gosub "",*windowmenualsizechange
	maxadr=stat
	pos ginfo(12)-xsiz(90),ysiz(0)
	objimage 1008,xsiz(0),ysiz(59),xsiz(0),ysiz(59),xsiz(0),ysiz(60)
	button gosub "",*windowmenualsizechange
	minadr=stat
	objimage -1

	pos xsiz(0),ysiz(0)
	objsize xsiz(150),ysiz(30)
	if startmainmenubuf=1:{
		objimage 1004,xsiz(0),ysiz(0),xsiz(0),ysiz(0),xsiz(0),ysiz(1)
	}else{
		objimage 1003,xsiz(0),ysiz(0),xsiz(0),ysiz(0),xsiz(0),ysiz(1)
	}
	button gosub "",*startmainmenuchange
	startmainmenuadr=stat

	if qwertydebag=1:{
		title "<デバッグ>No.6 :qwerty3xアプリケーションの描画中/Drawing qwerty3x application | qwerty3x ver."+qwertyver
	}
	if screenname=langstr(3001):{;ランチャー

	}else:if screenname=langstr(3005):{;qwerty3x 設定
		clientwindowx=ginfo(12)
		clientwindowy=ginfo(13)
	
		gsel 0003,0
		width xsiz(150),clientwindowy-ysiz(30),xsiz(0),ysiz(30)
		color 0,0,0
		boxf 
		font fontstr,fontsize,0,0
		objmode 2,1

		gosub *controlmakelist

		gsel 0003,2
		objimage -1
		gsel 0000,int(loadset("DISPCOLORMODE",settingdata))+1
	}else:if screenname=langstr(3002):{;スタートメインメニュー
		nowysize=ysiz(0)
		nowxsize=xsiz(0)
		animationspeed=45
		repeat animationspeed
			redraw 0
			setease ysiz(0),ginfo(13),ease_quartic_out
			nowysize=getease(cnt,animationspeed)
			boxfofmes int(loadset("DISPCOLORMODE",settingdata)),xsiz(0),ysiz(0),xsiz(150),nowysize

			setease xsiz(0),ginfo(12),ease_quartic_out
			nowxsize=getease(cnt,animationspeed)
			boxfofmes int(loadset("DISPCOLORMODE",settingdata)),xsiz(0),ginfo(13)-ysiz(40),nowxsize,ginfo(13)

			redraw 1
			await 8
		loop
		redraw 0

		line xsiz(150),ginfo(13),xsiz(150),ysiz(0)
		line ginfo(12),ginfo(13)-ysiz(40),xsiz(0),ginfo(13)-ysiz(40)

		clientwindowx=ginfo(12)
		clientwindowy=ginfo(13)

		bgscr 0001,ginfo(20),ginfo(21),2
		hchild.0=hwnd
		getwindowlong hwnd,-16
		setwindowlong hwnd,-16,stat|$CF0000
		setparent hchild.0,hclient
		getwindowlong hwnd,-16
		setwindowlong hwnd,-16,stat-$800000-$40000
		color 0,0,0
		boxf 
		font fontstr,fontsize,0,0
		objmode 2,1
		gsel 0001,0
		width (clientwindowx-xsiz(150)),(clientwindowy-ysiz(70)),xsiz(150),ysiz(30)

		startmainmenuposx=(ginfo(12)-(xsiz(150)*(ginfo(12)/xsiz(150))))/2

		nowstartmainposY=ysiz(10)
		pos startmainmenuposx,nowstartmainposY
		boxfofmes onezero(loadset("DISPCOLORMODE",settingdata)),startmainmenuposx,nowstartmainposY,startmainmenuposx+fontxsizedata.2+xsiz(10),nowstartmainposY+fontysizedata.2
		pos startmainmenuposx+xsiz(3),nowstartmainposY
		mes langstr(3003);・qwerty3x アプリケーション

		objsize xsiz(300),ysiz(30)
		nowstartmainpos=startmainmenuposx
		nowstartmainposY+=ysiz(40)	
		bufferbuttonY=ysiz(0)
		pos nowstartmainpos,nowstartmainposY
		objimage 1005,xsiz(0),bufferbuttonY,xsiz(0),bufferbuttonY,xsiz(300),bufferbuttonY
		button gosub "",*startqwertyapp
		qweapp.0=stat;ランチャー

		if nowstartmainpos+(xsiz(300)*2)>=ginfo(12):{
			nowstartmainpos=startmainmenuposx
			nowstartmainposY+=ysiz(30)
		}else{
			nowstartmainpos+=xsiz(300)
			;nowstartmainposYは変わらない
		}
		bufferbuttonY+=ysiz(30)
		pos nowstartmainpos,nowstartmainposY
		objimage 1005,xsiz(0),bufferbuttonY,xsiz(0),bufferbuttonY,xsiz(300),bufferbuttonY
		button gosub "",*startqwertyapp
		qweapp.1=stat;Windows 設定

		nowstartmainposY+=ysiz(40)
		pos startmainmenuposx,nowstartmainposY
		boxfofmes onezero(loadset("DISPCOLORMODE",settingdata)),startmainmenuposx,nowstartmainposY,startmainmenuposx+fontxsizedata.2+xsiz(10),nowstartmainposY+fontysizedata.2
		pos startmainmenuposx+xsiz(3),nowstartmainposY
		mes langstr(3004);・Windows アプリケーション

		objsize xsiz(300),ysiz(30)
		nowstartmainpos=startmainmenuposx
		nowstartmainposY+=ysiz(40)
		bufferbuttonY+=ysiz(30)
		pos nowstartmainpos,nowstartmainposY
		objimage 1005,xsiz(0),bufferbuttonY,xsiz(0),bufferbuttonY,xsiz(300),bufferbuttonY
		button gosub "",*startwindowsapp
		winapp.0=stat;タスクマネージャー

		winapp1cant=0
		if okfullmode=0{winapp1cant=1}
		if okfullmode=1&windowsver="NT6.1"{winapp1cant=1}
		if winapp1cant!1:{
			if nowstartmainpos+(xsiz(300)*2)>=ginfo(12):{
				nowstartmainpos=startmainmenuposx
				nowstartmainposY+=ysiz(30)
			}else{
				nowstartmainpos+=xsiz(300)
				;nowstartmainposYは変わらない
			}
			bufferbuttonY+=ysiz(30)
			pos nowstartmainpos,nowstartmainposY
			objimage 1005,xsiz(0),bufferbuttonY,xsiz(0),bufferbuttonY,xsiz(300),bufferbuttonY
			button gosub "",*startwindowsapp
			winapp.1=stat;Windows 設定
		}

		if nowstartmainpos+(xsiz(300)*2)>=ginfo(12):{
			nowstartmainpos=startmainmenuposx
			nowstartmainposY+=ysiz(30)
		}else{
			nowstartmainpos+=xsiz(300)
			;nowstartmainposYは変わらない
		}
		bufferbuttonY+=ysiz(30)
		pos nowstartmainpos,nowstartmainposY
		objimage 1005,xsiz(0),bufferbuttonY,xsiz(0),bufferbuttonY,xsiz(300),bufferbuttonY
		button gosub "",*startwindowsapp
		winapp.2=stat;ファイル名を指定して実行

		if nowstartmainpos+(xsiz(300)*2)>=ginfo(12):{
			nowstartmainpos=startmainmenuposx
			nowstartmainposY+=ysiz(30)
		}else{
			nowstartmainpos+=xsiz(300)
			;nowstartmainposYは変わらない
		}
		bufferbuttonY+=ysiz(30)
		pos nowstartmainpos,nowstartmainposY
		objimage 1005,xsiz(0),bufferbuttonY,xsiz(0),bufferbuttonY,xsiz(300),bufferbuttonY
		button gosub "",*startwindowsapp
		winapp.3=stat;コントロールパネル

		gsel 0001,2
		objimage -1
		gsel 0000,int(loadset("DISPCOLORMODE",settingdata))+1
		
	}

	redraw 1
	enablewindow hclient,1

	gsel 0000,int(loadset("DISPCOLORMODE",settingdata))+1

	return

#include "module/include code_3_buffer.hsp"
#include "module/include code_4_controlmakescreen.hsp"

*startmainmenuchange;スタートメインメニューとの切り替え
	if startmainmenubuf=0:{
		bufscreenname=screenname
		screenname=langstr(3002);スタートメインメニュー
		startmainmenubuf=1
	}else{
		screenname=bufscreenname
		startmainmenubuf=0
	}

	gosub *makescreen
	return

*windowmenualsizechange;手動での最大化、最小化の適用
	if maxadr=stat:{
		IsZoomed hwnd
		iszoom=stat
		if iszoom=1:{
			sendmsg hwnd,$112,$F120;SC_RESTORE
		}else{
			sendmsg hwnd,$112,$F030;SC_MAXIMIZE
		}
	}else:if minadr=stat:{
		sendmsg hwnd,$112,$F020;SC_MINIMIZE
	}
	
	return

*startqwertyapp
	if qweapp.0=stat:{;ランチャー
		screenname=langstr(3001)
	}else:if qweapp.1=stat:{;qwerty3x 設定
		screenname=langstr(3005)
	}
	startmainmenubuf=0

	gosub *makescreen

	goto *systemrepeat

*startwindowsapp
	if winapp.0=stat:{;タスクマネージャー
		keybd_event $11,0,0,0
		keybd_event $10,0,0,0
		keybd_event $1B,0,0,0
		keybd_event $11,0,2,0
		keybd_event $10,0,2,0
		keybd_event $1B,0,2,0
	}else:if winapp.1=stat:{;Windows 設定
		exec "cmd.exe /c Explorer.exe shell:AppsFolder\\Windows.ImmersiveControlPanel_cw5n1h2txyewy!microsoft.windows.immersivecontrolpanel",2
	}else:if winapp.2=stat:{;ファイル名を指定して実行
		findwindow "shell_traywnd",0
		htraywnd=stat
		sendmsg htraywnd,$111,$191
	}else:if winapp.3=stat:{;コントロールパネル
		exec "control"
	}

	return

//////////////////////////////////////////////////////////////////////
//システムスクリプト
//////////////////////////////////////////////////////////////////////

*sizechange;ウィンドウのサイズが変わったとき
	redraw 0
	color 0,0,0
	boxf
	pos xsiz(0),ysiz(0)
	boxfofmes int(loadset("DISPCOLORMODE",settingdata)),xsiz(0),ysiz(0),ginfo(12),ysiz(30)
	line ginfo(12),ysiz(30),xsiz(0),ysiz(30)
		
	size=0,0,(ginfo(12)-xsiz(30)),0
	resizeobj endadr,size,2
	size=0,0,(ginfo(12)-xsiz(60)),0
	resizeobj maxadr,size,2
	size=0,0,(ginfo(12)-xsiz(90)),0
	resizeobj minadr,size,2

	if screenname=langstr(3002):{;スタートメインメニュー
		gosub *startmenuui
	}else:if screenname=langstr(3005):{;qwerty3x 設定
		gosub *controlui
	}
	redraw 1

	return

*movechange;ウィンドウの位置が変わったとき
	redraw 0
	IsZoomed hwnd
	if iszoom!stat:{
		iszoom=stat
		clrobj maxadr,maxadr
		pos ginfo(12)-xsiz(60),ysiz(0)
		objsize xsiz(30),ysiz(30)
		if iszoom=0:{
			objimage 1008,xsiz(30),ysiz(59),xsiz(30),ysiz(59),xsiz(30),ysiz(60)
		}else{
			objimage 1008,xsiz(60),ysiz(59),xsiz(60),ysiz(59),xsiz(60),ysiz(60)
		}
		button gosub "",*windowmenualsizechange
		maxadr=stat
	}else{
		iszoom=stat
	}

	gosub *sizechange
	redraw 1

	return

*onclickjump;ウィンドウがクリックされたとき(onclick)
	if wparam=1:{
		sendmsg hwnd,$A1,2
	}

	return

*OnNCHITTEST;タイトルバーの色を変更
	dim area
	mposx = ginfo_mx - ginfo_wx1
	mposy = ginfo_my - ginfo_wy1
	if (mposx <= BORDERWIDTH)             : area |= LEFT
	if (mposx >= ginfo_winx - BORDERWIDTH): area |= RIGHT
	if (mposy <= BORDERWIDTH)             : area |= TOP
	if (mposy >= ginfo_winy - BORDERWIDTH): area |= BOTTOM
	if (area & LEFT) {
		if (area & TOP)    : return HTTOPLEFT
		if (area & BOTTOM) : return HTBOTTOMLEFT
		return HTLEFT
	}
	if (area & RIGHT) {
		if (area & TOP)    : return HTTOPRIGHT
		if (area & BOTTOM) : return HTBOTTOMRIGHT
		return HTRIGHT
	}
	if (area & TOP)    : return HTTOP
	if (area & BOTTOM) : return HTBOTTOM
	if (mposy <= CAPTIONHEIGHT) : return HTCAPTION
	
	return HTCLIENT

*onminmax;ウィンドウの最小サイズの指定
	dupptr minmaxinfo,lparam,40,4
	minmaxinfo(6)=xsiz(500)
	minmaxinfo(7)=ysiz(500)

	return 0
	
*systemrepeat;システムメインループ
	//システム
	gsel 0
	IsZoomed hwnd
	if iszoom!stat:{
		iszoom=stat
		clrobj maxadr,maxadr
		pos ginfo(12)-xsiz(60),ysiz(0)
		objsize xsiz(30),ysiz(30)
		if iszoom=0:{
			objimage 1008,xsiz(30),ysiz(59),xsiz(30),ysiz(59),xsiz(30),ysiz(60)
		}else{
			objimage 1008,xsiz(60),ysiz(59),xsiz(60),ysiz(59),xsiz(60),ysiz(60)
		}
		button gosub "",*windowmenualsizechange
		maxadr=stat
	}else{
		iszoom=stat
	}
	IsIconic hwnd
	isicon=stat

	year=strf("%04d",gettime(0));年=year
	month=strf("%02d",gettime(1));月=month
	week=gettime(2)
	if week=0:{
		weekstr=langstr(4001);日
	}else:if week=1:{
		weekstr=langstr(4002);月
	}else:if week=2:{
		weekstr=langstr(4003);火
	}else:if week=3:{
		weekstr=langstr(4004);水
	}else:if week=4:{
		weekstr=langstr(4005);木
	}else:if week=5:{
		weekstr=langstr(4006);金
	}else:if week=6:{
		weekstr=langstr(4007);土
	};曜日番号=week 曜日=weekstr
	day=strf("%02d",gettime(3));日=day
	hour=gettime(4)
	if useampm=0:{
		hour=strf("%02d",hour)
	}else:if useampm=1:{
		if gettime(4)<12:{
			hour=strf("%02d",hour)
			hour="am"+hour
		}else:if gettime(4)>12&gettime(4)<=23:{
			hour=hour-12
			hour=strf("%02d",hour)
			hour="pm"+hour
		}
	}
	minute=strf("%02d",gettime(5))
	second=strf("%02d",gettime(6))

	dim system_power_status,3
	getsystempowerstatus varptr(system_power_status)
	batteryper=peek(system_power_status.0,2)
	batteryflag=peek(system_power_status.0,1)
	acstat=peek(system_power_status.0,0)
	batterytime=system_power_status.1
	//
	batterytimeday=(batterytime/86400)
	batterytimehour=((batterytime\86400)/3600)
	batterytimeminute=(((batterytime\86400)\3600)/60)
	batterytimesecond=(((batterytime\86400)\3600)\60)
	batterytimestr=""+langstr(4008)+" ";残り
	batterytimestr2=langstr(4008);残り
	if batterytimeday!0:{
		batterytimestr=batterytimestr+batterytimeday+langstr(4009);日
		batterytimestr2=batterytimestr2+batterytimeday+":"
	}
	if batterytimehour!0:{
		batterytimestr=batterytimestr+batterytimehour+langstr(4010);時間
	}
	batterytimestr2=batterytimestr2+strf("%02d",batterytimehour)
	if batterytimeminute!0:{
		batterytimestr=batterytimestr+batterytimeminute+langstr(4011);分
	}
	batterytimestr2=batterytimestr2+":"+strf("%02d",batterytimeminute)
	if batterytimesecond!0:{
		batterytimestr=batterytimestr+batterytimesecond+langstr(4012);秒
	}
	batterytimestr2=batterytimestr2+":"+strf("%02d",batterytimesecond)

	//ジャンプ
	onexit *shutdownwindowssystem
	onclick gosub *onclickjump

	oncmd gosub *sizechange,$005;WM_SIZE
	oncmd gosub *OnNCHITTEST, $084;WM_NCHITTEST
	oncmd gosub *movechange,$003;WM_MOVE
	oncmd gosub *onminmax,$024;WM_GETMINMAXINFO

	//アプリ
	if screenname=langstr(3002):{;スタートメインメニュー
		gosub *startmenuui
	}
	if screenname=langstr(3005):{;qwerty3x 設定
		gosub *controlui
	}

	//システム
	titlebarstr=""
	if startmainmenubuf=1:{;スタートメインメニュー
		titlebarstr=""+screenname+"/"+bufscreenname
	}else{
		titlebarstr=""+screenname
	}
	if qwertydebag=1:{
		titlebarstr="<デバッグ> posx:"+ginfo(4)+" posy:"+ginfo(5)+" sizx:"+ginfo(12)+" sizy:"+ginfo(13)+" "+titlebarstr
	}
	title titlebarstr+" | "+langstr(0001);qwerty3x ver.%qwertyver%

	saveset "ISZOOMED",settingdata,str(iszoom)
	if iszoom!1:{
		saveset "WINDOW_POSX",settingdata,str(ginfo(4))
		saveset "WINDOW_POSY",settingdata,str(ginfo(5))
		saveset "WINDOW_SIZX",settingdata,str(ginfo(12))
		saveset "WINDOW_SIZY",settingdata,str(ginfo(13))
	}

	gosub *langstrrepupdate

	if isicon=1:{
		await 1000
	}else{
		await (1000/30)
	}
	goto *systemrepeat

*startmenuui
	redraw 0
	gsel 0001
	mousewheelbuf=mousew
	if mousewheelbuf>0:{
		mousewheelbuttonbuf=1
		groll xsiz(0),ginfo(9)-ysiz(30)
		if ginfo(9)=mousewheelginfobuf:{
			mousewheelbuttonbuf=0
		}
		mousewheelginfobuf=ginfo(9)
	}else:if mousewheelbuf<0:{
		if (ginfo(9)+ysiz(40))<nowstartmainposY+ysiz(30):{
			mousewheelbuttonbuf=1
			groll xsiz(0),ginfo(9)+ysiz(30)
			if ginfo(9)=mousewheelginfobuf:{
				mousewheelbuttonbuf=0
			}
			mousewheelginfobuf=ginfo(9)
		}
	}
	if mousewheelbuf!0:{
		;objsel qweapp.0
		;objsel qweapp.1
		;objsel winapp.0
		;objsel winapp.1
		;objsel winapp.2
		;objsel winapp.3
	}
	gsel 0000
	if mousewheelbuf!0:{
		objsel startmainmenuadr
	}
	
	boxfofmes int(loadset("DISPCOLORMODE",settingdata)),xsiz(0),ysiz(0),xsiz(150),ginfo(13)
	boxfofmes int(loadset("DISPCOLORMODE",settingdata)),xsiz(0),ginfo(13)-ysiz(40),ginfo(12),ginfo(13)
	if batteryflag!128:{;バッテリー情報なしではないとき
		if acstat=1:{;充電中のとき
			pos xsiz(0),ginfo(13)-ysiz(90)
			mes ""+langstr(4013)+": "+batteryper+"%";AC電源
			if batteryper=100:{
				mes langstr(4014);バッテリー充電完了
			}else{
				mes langstr(4015);バッテリー充電中
			}
			boxf xsiz(0),ginfo(13)-ysiz(50),int(double(xsiz(150.0))*(double(batteryper)/100)),ginfo(13)-ysiz(45)
		}else{
			pos xsiz(0),ginfo(13)-ysiz(90)
			if acstat=1:{
				mes ""+langstr(4013)+": "+batteryper+"%";AC電源
			}else{
				mes ""+langstr(4016)+": "+batteryper+"%";バッテリー
			}
			if batterytime=-1:{
				if batteryper=100:{
					mes langstr(4014);バッテリー充電完了
				}else{
					mes langstr(4017);残り時間予測中
				}
			}else{
				mes batterytimestr2
			}
			boxf xsiz(0),ginfo(13)-ysiz(50),int(double(xsiz(150.0))*(double(batteryper)/100)),ginfo(13)-ysiz(45)
		}
	}else{
		boxf xsiz(0),ginfo(13)-ysiz(50),xsiz(150),ginfo(13)-ysiz(45)
	}
	line xsiz(150),ginfo(13),xsiz(150),ysiz(0)
	line ginfo(12),ginfo(13)-ysiz(40),xsiz(0),ginfo(13)-ysiz(40)
	fontysizepos=(ysiz(40)-fontysizedata.1)/2
	pos xsiz(160),(ginfo(13)-ysiz(40))+fontysizepos
	font fontstr,fontsize+ysiz(7),0,0;ysiz(22)
	mes ""+year+"/"+month+"/"+day+"("+weekstr+") "+hour+":"+minute+":"+second
	font fontstr,fontsize,0,0;ysiz(15)

	fontxsizepos=(xsiz(150)-fontxsizedata.1)/2
	pos fontxsizepos,(ginfo(13)-ysiz(40))+fontysizepos
	font fontstr,fontsize+ysiz(7),1+2,0;ysiz(22)
	mes "qwerty3x"
	font fontstr,fontsize,0,0;ysiz(15)

	clientwindowx=ginfo(12)
	clientwindowy=ginfo(13)
	redraw 1
	gsel 0001,0
	redraw 0
	
	width (clientwindowx-xsiz(150)),(clientwindowy-ysiz(70)),xsiz(150),ysiz(30)

		color 0,0,0
		boxf
	
		startmainmenuposx=(ginfo(12)-(xsiz(150)*(ginfo(12)/xsiz(150))))/2
		
		nowstartmainposY=ysiz(10)
		pos startmainmenuposx,nowstartmainposY
		boxfofmes onezero(loadset("DISPCOLORMODE",settingdata)),startmainmenuposx,nowstartmainposY,startmainmenuposx+fontxsizedata.2+xsiz(10),nowstartmainposY+fontysizedata.2
		pos startmainmenuposx+xsiz(3),nowstartmainposY
		mes langstr(3003);・qwerty3x アプリケーション
	
		nowstartmainpos=startmainmenuposx
		nowstartmainposY+=ysiz(40)
		bufferbuttonY=ysiz(0)
		size=0,0,nowstartmainpos,nowstartmainposY-ginfo(9)
		resizeobj qweapp.0,size,2;ランチャー
		if nowstartmainpos+(xsiz(300)*2)>=ginfo(12):{
			nowstartmainpos=startmainmenuposx
			nowstartmainposY+=ysiz(30)
		}else{
			nowstartmainpos+=xsiz(300)
			;nowstartmainposYは変わらない
		}
		bufferbuttonY+=ysiz(30)
		size=0,0,nowstartmainpos,nowstartmainposY-ginfo(9)
		resizeobj qweapp.1,size,2
	
		nowstartmainposY+=ysiz(40)
		pos startmainmenuposx,nowstartmainposY
		boxfofmes onezero(loadset("DISPCOLORMODE",settingdata)),startmainmenuposx,nowstartmainposY,startmainmenuposx+fontxsizedata.2+xsiz(10),nowstartmainposY+fontysizedata.2
		pos startmainmenuposx+xsiz(3),nowstartmainposY
		mes langstr(3004);・Windows アプリケーション
	
		nowstartmainpos=startmainmenuposx
		nowstartmainposY+=ysiz(40)
		bufferbuttonY+=ysiz(30)
		size=0,0,nowstartmainpos,nowstartmainposY-ginfo(9)
		resizeobj winapp.0,size,2
		if winapp1cant!1:{
			if nowstartmainpos+(xsiz(300)*2)>=ginfo(12):{
				nowstartmainpos=startmainmenuposx
				nowstartmainposY+=ysiz(30)
			}else{
				nowstartmainpos+=xsiz(300)
				;nowstartmainposYは変わらない
			}
			bufferbuttonY+=ysiz(30)
			size=0,0,nowstartmainpos,nowstartmainposY-ginfo(9)
			resizeobj winapp.1,size,2
		}
		if nowstartmainpos+(xsiz(300)*2)>=ginfo(12):{
			nowstartmainpos=startmainmenuposx
			nowstartmainposY+=ysiz(30)
		}else{
			nowstartmainpos+=xsiz(300)
			;nowstartmainposYは変わらない
		}
		bufferbuttonY+=ysiz(30)
		size=0,0,nowstartmainpos,nowstartmainposY-ginfo(9)
		resizeobj winapp.2,size,2
		if nowstartmainpos+(xsiz(300)*2)>=ginfo(12):{
			nowstartmainpos=startmainmenuposx
			nowstartmainposY+=ysiz(30)
		}else{
			nowstartmainpos+=xsiz(300)
			;nowstartmainposYは変わらない
		}
		bufferbuttonY+=ysiz(30)
		size=0,0,nowstartmainpos,nowstartmainposY-ginfo(9)
		resizeobj winapp.3,size,2
	
	redraw 1
	gsel 0000

	return

//////////////////////////////////////////////////////////////////////
//設定読み書きスクリプト
//////////////////////////////////////////////////////////////////////

*loadingsettingfile;設定・言語ファイルを読み込む
	settingfile=""+qwertycur+"\\settingdata"
	languagefile=""+qwertycur+"\\languagedata"

	;設定ファイル読み込み
	chdir qwertycur
	dirlist dummy,settingfile,5
	if stat=0:{
		mkdir "settingdata"
	}
	chdir settingfile
	exist "setting.q3sf"
	if strsize=-1:{
		settingdata=defaultsettingdata

		encword passwordkey,settingdata,encsettingdata
		notesel encsettingdata
		notesave "setting.q3sf"
	}
	notesel decsettingdata
	noteload "setting.q3sf"
	decword passwordkey,decsettingdata,settingdata

	;言語ファイル読み込み
	chdir qwertycur
	dirlist dummy,languagefile,5
	if stat=0:{
		mkdir "languagedata"
	}
	chdir languagefile
	exist ""+loadset("LANGUAGE",settingdata)+".txt"
	if strsize=-1:{
		dialog "The language file could not be found.\nThe language setting eill be changed to Japanese.\n\n言語ファイルが見つかりませんでした。\n言語設定を日本語に変更します。",0,"qwerty3x ver."+qwertyver
		saveset "LANGUAGE",settingdata,"ja_JP"
		exist ""+loadset("LANGUAGE",settingdata)+".txt"
		if strsize=-1:{
			dialog "The Japanese language file could not be found.\nStartup Process cannot continue.\nqwerty3x will now exit.\n\n日本語の言語ファイルが見つかりませんでした。\n起動処理を続行できません。\nqwerty3xを終了します。",0,"qwerty3x ver."+qwertyver
			end 
		}
	}
	notesel languagedata
	noteload ""+loadset("LANGUAGE",settingdata)+".txt"

	return

*langstrrepupdate;言語ファイルでの変換記号先の変数を指定する
	;言語ファイル変換表の作成
	langstrrep=""
	langstrrep+="%qwertyver%==="+qwertyver+""
	langstrrep+="\n%windowsname%==="+windowsname+""

	return

*saveingsettingfile;設定ファイルを保存する
	chdir qwertycur
	dirlist dummy,settingfile,5
	if stat=0:{
		mkdir "settingdata"
	}
	if settingdata="":{
		settingdata=defaultsettingdata
	}
	
	chdir settingfile
	encword passwordkey,settingdata,encsettingdata
	notesel encsettingdata
	notesave "setting.q3sf"

	return

//////////////////////////////////////////////////////////////////////
//シャットダウンスクリプト
//////////////////////////////////////////////////////////////////////

*shutdownwindowssystem;終了するとき(onexit)
	if iparam=0:{
		;ユーザーの意思で終了
		shutdownmode=0
		restartmode=0
	}else:if iparam=0:{
		;Windowsシャットダウンで終了
		shutdownmode=1
		restartmode=0
	}

	goto *systemshutdown

*systemshutdown;qwerty3xを終了する
	if endadr=stat:{
		;ユーザーの意志で終了
		shutdownmode=0
		restartmode=0
	}
	title ""+langstr(2001)+" | qwerty3x ver."+qwertyver;終了中

	gosub *saveingsettingfile

	end